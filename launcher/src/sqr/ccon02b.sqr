!*****************************************************************************
!  CONTRACT CLOSURE BATCH REPORT                                             *
!*****************************************************************************  

!*****************************************************************************
!                                                                            *
! PROGRAM NAME:         ccon02b.sqr                                          *
!                                                                            *
! PROGRAM LOCATION:     /caps/users/mccleabg/work/sqr                        *
!                                                                            *
!                                                                            *
! DATE:                 10/18/95                                             *
!                                                                            *
! PROGRAMMER NAME:      Brad McCleary                                        *
!                                                                            *
! PROGRAM DESCRIPTION:  The Contract Closure Batch Report lists all of the   *
!                       current contracts approaching their Closure Dates    * 
!                       based upon their procurement method.                 *  
!*****************************************************************************

!*****************************************************************************
!                                                                            *
! PROGRAM MODIFICATION SECTION                                               *
! ----------------------------                                               *
!                                                                            *
!   DATE     PROGRAMMER                       MODIFICATION                   *
! --------   ----------   -------------------------------------------------  *
! 10/18/95   B. McCleary  Initial Programming                                *
! 11/05/96   K. Cys       ENH - Added code for Name Person Full enhancement. *
!                                                                            *
!*****************************************************************************

!*****************************************************************************
!                                                                            *
! REPORT PROCESSING SECTION                                                  *
!                                                                            *
!       This procedure defines the beginning and end of the report proc-     *
!       essing. Housekeeping initializes all report values, Processing       *
!       processes the body of the report, and Wrap-Up prints the end of re-  *
!       port message and other wrap up functions.                            *
!                                                                            *
!*****************************************************************************

BEGIN-REPORT

    do HOUSEKEEPING

    do PROCESS-REPORT        

    do WRAP-UP

END-REPORT

!*****************************************************************************
!  SETUP-SECTION                                                             *
!                                                                            *
!       This section sets up common parameters including page size and       *
!       printer as well as input parameters                                  *
!                                                                            *
!*****************************************************************************
BEGIN-SETUP

    #ifdef ORACLE                       !compiler directive
        ask dbname 'Database Name'
        use {dbname}
    #endif

    #INCLUDE 'setup01a.sqc'             ! control codes for Landscape 
                                        ! Batch Reports              

    ask CURRENT_DATE                    ! input parameter 
                                       

END-SETUP

!*****************************************************************************
!                                                                            *
! HEADER                                                                     *
!                                                                            *
!       These commands will be processed every time SQR writes a page. They  *
!       include the report title, date, time, page number, report id, and    *
!       column headers. XXX lines are reserved to print the heading at the   *
!       top of each page. This includes blank lines. A common setup header   *
!       has been created called XXXXXXXXXXXXXXXXXXX for landscape or portrait*
!       reports.                                                             *
!                                                                            *
!*****************************************************************************

BEGIN-HEADING 9 

  #INCLUDE 'header01.sqc'               !Include file with Header Information
                    !Specific to landscape batch reports

print $MONTH_DATE_I_DO_NOT_KNOW                       (+1,1)

print 'Region: '             		 (+2,1)
print $previous_region             	 (+0,9)
print 'Contract Manager: '         	 (+1,1)
if $prev_contract_suff <> ''  !***ENH***
   let $con_suff_nm = $previous_contract_mgr || ', ' || $prev_contract_suff
   print $con_suff_nm                    (+0,19)
   move '' to $prev_contract_suff
else
   print $previous_contract_mgr          (+0,19)
end-if
print 'Contract ID'           	 	 (+2,1) 
print 'Period Nbr'            		 (+0,17)
print 'Resource Name'    	         (+0,33)
print 'Start Date'              	 (+0,68)
print 'Closure Date'               	 (+0,82)
print 'Term Date'              		 (+0,99)
print 'Program'                 	 (+0,114)
print 'Period Status'               	 (+0,135)
print 'Procurement Type'            	 (+0,155)
print '_'                   		 (+1,1,176) fill


END-HEADING

!*****************************************************************************
!                                                                            *
! FOOTER                                                                     *
!                                                                            *
!       These commands will be processed every time SQR writes a page. They  *
!       include a footer that must be in place in all pages of some reports  *
!                                                                            *
!*****************************************************************************

!BEGIN-FOOTING 2

!  #INCLUDE 'footer01.sqc'       ! Include file with footer information
!                    !Specific to landscape batch reports

!END-FOOTING

!****************************************************************************
!                                                                           *
!   HOUSEKEEPING                                                            *
!                                                                           *
!       Set environment variables, initialize report id, initialize title   *
!       id, initialize date and time, initialize variables, and define      *
!       constants                                                           *
!                                                                           *
!       CALLED BY: BEGIN-REPORT                                             *
!       CALLS:     GET-CASE-NAME-NUMBER                                     * 
!****************************************************************************

BEGIN-PROCEDURE HOUSEKEEPING

   #INCLUDE 'setenv.sqc'       !Set printer environment

   move '{CURRENT_DATE}'                to $input_current_date
  
   move 'ccon02b'                       to $ReportId
   move 'CONTRACT CLOSURE BATCH REPORT' to $ReportTitle
   move 'CONTRACTS COMING DUE'          to $ReportSubHeading

   move 'CSB'				to $csb
   move 'CNG'				to $cng
   move 'NCN'				to $ncn
   move 'PEN'				to $pen
   move 'SOS'				to $sos
   move 'SPC'				to $spc

END-PROCEDURE HOUSEKEEPING

!****************************************************************************
!                                                                           *
!   PROCESS-REPORT                                                          *
!                                                                           *
!      This procedure includes the main processing logic of the report.     *
!                                                                           *
!       CALLED BY  : BEGIN-REPORT                                           *
!       CALLS      : GET-CONTRACTS                                          *
!****************************************************************************

BEGIN-PROCEDURE PROCESS-REPORT 

    do GET-CONTRACTS

END-PROCEDURE PROCESS-REPORT                   

!****************************************************************************
!                                                                           *
!   GET-CONTRACTS                                                           *
!                                                                           *
!       This procedure will get all the codified information for a contact  *
!       including the worker name, but excluding the persons related to     *
!       the contract.                                                       *
!                                                                           * 
!       CALLED BY  : PROCESS REPORT                                         *
!       CALLS      : FIRST-CONTRACT-PRINT, PRINT-CONTRACTS                  *
!****************************************************************************

BEGIN-PROCEDURE GET-CONTRACTS

    move 0      to #count_1             ! initialize the counter

BEGIN-SELECT ON-ERROR=SqlErrorHandle

A.CD_CNTRCT_REGION
    move &A.CD_CNTRCT_REGION                    to $region
    
B.NM_NAME_FIRST &contract_mgr_first             !***ENH***
    move &contract_mgr_first                    to $contract_mgr_first

B.NM_NAME_MIDDLE &contract_mgr_middle
    move &contract_mgr_middle                   to $contract_mgr_middle

B.NM_NAME_LAST &contract_mgr_last
    move &contract_mgr_last                     to $contract_mgr_last

CSUFFIX2.decode                                 !***ENH***
    move &CSUFFIX2.decode                       to $contract_suff

A.ID_CONTRACT
    move &A.ID_CONTRACT                         to #id_contract
    
C.NBR_CNPER_PERIOD
    move &C.NBR_CNPER_PERIOD                    to $per_num
    
D.NM_RESOURCE
    move &D.NM_RESOURCE                         to $resource
    
to_char(C.DT_CNPER_START, 'MM/DD/YYYY') &date1    
    move &date1                                 to $start_dt
    
to_char(C.DT_CNPER_CLOSURE, 'MM/DD/YYYY') &date2    
    move &date2                                 to $closure_dt
    
to_char(C.DT_CNPER_TERM, 'MM/DD/YYYY') &date3
    move &date3                                 to $term_dt
    
A.CD_CNTRCT_PROGRAM_TYPE           
    move &A.CD_CNTRCT_PROGRAM_TYPE              to $prog_type 
    
CCONSTAT.decode             
    move &CCONSTAT.decode                       to $status
    
A.CD_CNTRCT_PROCURE_TYPE     
    move &A.CD_CNTRCT_PROCURE_TYPE              to $proc_type

    do GET-CODES

    let #count_1 = #count_1 +1

    !***ENH***
    if $contract_mgr_middle = ''
       let $contract_mgr = $contract_mgr_first || ' ' || $contract_mgr_last 
    else
       let $contract_mgr = $contract_mgr_first || ' ' || $contract_mgr_middle || ' ' || $contract_mgr_last 
    end-if
     
    if #count_1 = 1
    move $region to $previous_region
    move $contract_mgr to $previous_contract_mgr
    move $contract_suff to $prev_contract_suff
        do PRINT-CONTRACTS
    else    
    	if    ($region <> $previous_region 
       	     or $contract_mgr <> $previous_contract_mgr)
          	 NEW-PAGE
                  move $region to $previous_region
                  move $contract_mgr to $previous_contract_mgr
		  move $contract_suff to $prev_contract_suff
                  do PRINT-CONTRACTS 
        else
                  do PRINT-CONTRACTS
        end-if
    end-if    

FROM    CONTRACT        A,
        NAME            B, !***ENH***
        CONTRACT_PERIOD C,
        CAPS_RESOURCE   D,
	CCONSTAT        CCONSTAT,
	CSUFFIX2        CSUFFIX2  !***ENH***

WHERE   A.ID_CONTRACT       = C.ID_CONTRACT
AND     A.ID_CNTRCT_MANAGER = B.ID_PERSON
AND     A.ID_RESOURCE       = D.ID_RESOURCE
AND     CCONSTAT.CODE(+)    = C.CD_CNPER_STATUS
AND     CSUFFIX2.CODE(+)    = B.CD_NAME_SUFFIX !***ENH***

AND ( 
    ((trunc(to_date($input_current_date,'MM/DD/YYYY')) <= C.DT_CNPER_CLOSURE) 
 AND (C.DT_CNPER_CLOSURE >= (trunc(to_date($input_current_date,'MM/DD/YYYY')) + 90))
 AND (A.CD_CNTRCT_PROCURE_TYPE IN ($csb, $cng)))
     

OR  
    ((trunc(to_date($input_current_date,'MM/DD/YYYY')) <= C.DT_CNPER_CLOSURE) 
 AND (C.DT_CNPER_CLOSURE >= (trunc(to_date($input_current_date,'MM/DD/YYYY')) + 180)) 
 AND (A.CD_CNTRCT_PROCURE_TYPE IN ($ncn, $pen, $sos, $spc)))
     )       

ORDER BY    A.CD_CNTRCT_REGION, B.NM_NAME_LAST, A.ID_CONTRACT            

END-SELECT    

END-PROCEDURE GET-CONTRACTS 

!****************************************************************************
!                                                                           *
!   GET-CODES                                                               *
!                                                                           *
!       This procedure will get all the codified information for a contact  *
!       including the worker name, but excluding the persons related to     *
!       the contract.                                                       *
!                                                                           * 
!       CALLED BY  : GET-CONTRACTS                                          *
!       CALLS      : NONE                                                   *
!****************************************************************************

BEGIN-PROCEDURE GET-CODES

BEGIN-SELECT ON-ERROR=SqlErrorHandle

CCONPROG.decode &dec1   
    move &dec1           	                   to $prog_dec
    
CCONPROC.decode &dec2
    move &dec2   	                           to $proc_dec  
    
FROM	CCONPROG    CCONPROG,
        CCONPROC    CCONPROC

WHERE   CCONPROG.CODE(+) = $prog_type                
AND     CCONPROC.CODE(+) = $proc_type              
        
END-SELECT    

END-PROCEDURE GET-CODES

!****************************************************************************
!                                                                           *
!   PRINT-CONTRACTS                                                         *
!                                                                           *
!       This procedure prints the contact log.                              *
!                                                                           * 
!       CALLED BY  : GET-CONTRACTS                                          *
!       CALLS      : NONE                                                   *
!****************************************************************************


BEGIN-PROCEDURE PRINT-CONTRACTS   

print #id_contract              (+1,1) edit 9999999999
print $per_num                  (+0,17)
print $resource                 (+0,33)
print $start_dt                 (+0,68)
print $closure_dt               (+0,82)
print $term_dt                  (+0,99)
print $prog_dec                 (+0,114)
print $status                   (+0,135)
print $proc_dec                 (+0,155)

END-PROCEDURE PRINT-CONTRACTS   

!****************************************************************************
!                                                                           *
!   WRAP-UP                                                                 *
!                                                                           *
!       This procedure prints the messages for the end of the report as well*
!       as over all totals.                                                 *
!                                                                           * 
!       CALLED BY  : BEGIN-REPORT                                           *
!       CALLS      : NONE                                                   *
!****************************************************************************

BEGIN-PROCEDURE WRAP-UP

   NEW-PAGE

END-PROCEDURE WRAP-UP


!***********************************************************************
!                                                                      *
! SqlErrorHandle                                                       *
!                                                                      *
!      This section handles formatting of messages for SQL errors.     *
!      When an SQL error is encounterd, the program will write to      *   
!      the log file, program processing does not stop for SQL errors.  *
!                                                                      *
!  CALLS:  None                                                        *
!                                                                      *
!***********************************************************************

BEGIN-PROCEDURE SqlErrorHandle 

  print 'SQL error - Processing Terminated'               (+1,2)
  print $error                                            (+1,2)
  print 'ERROR: SQL error halted loader processing'       (+0)
  print $error                                            (+1,2)
  print 'SQL Error: '                                     (+0)
  print $sql-error                                        (+0)
  print $error                                            (+1,2)
  print 'SQL Status: '                                    (+0)
  print #sql-status                                       (+0)

  STOP

END-PROCEDURE SqlErrorHandle


!****************************************************************************
!                                                                           *
!                                INCLUDE SECTION                            *
!                                                                           *
!   Include all files that contain common modules or procedures that        *
!   will be used in the report                                              *
!****************************************************************************


#INCLUDE 'cfpstamp.sqc'
