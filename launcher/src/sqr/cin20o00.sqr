!*****************************************************************************
!  EVENT LIST                                                                *
!*****************************************************************************  

!*****************************************************************************
!                                                                            *
! PROGRAM NAME:         cin20o00.sqr                                         *
!                                                                            *
! PROGRAM LOCATION:     /caps/users/dickmaec/work/sqr                        *
!                                                                            *
! DATE:                 12/07/2004                                           *
!                                                                            *
! PROGRAMMER NAME:      Eric Dickman                                         *
!                                                                            *
! PROGRAM DESCRIPTION:  The Initial Service Level Authorization report will  *
!                       be located on the Level of Care Detail page.  It will* 
!                        be used to inform the provider of the initial servic* 
!                       level authorized for the child, and will be launched *
!                       from the report section on the Person Level of Care  *
!                       page.  To view the Initial Service Level             *
!                       Authorization Report, the user will select the report*
!                       from the reports drop-down menu, click the 'Launch'  *
!                       push button, navigate to the 'Reports' first level   *
!                       tab, and then click on the Initial Service Level     *
!                       Authorization hyperlink.  The report will open in    *
!                       display only mode.                                   *
!                                                                            *
!*****************************************************************************

!*****************************************************************************
!                                                                            *
! PROGRAM MODIFICATION SECTION                                               *
! ----------------------------                                               *
!                                                                            *
!   DATE     PROGRAMMER                       MODIFICATION                   *
! --------   ----------   -------------------------------------------------  *
! 12/08/2004 Eric Dickman Initial creation of document.                      *
!	03/29/2005 Eric Dickman Add Disclaimer                                     * 
!                 																													 *	
!																																						 *	
!                                                                            *
!*****************************************************************************

!*****************************************************************************
!                                                                            *
! REPORT PROCESSING SECTION                                                  *
!                                                                            *
!       This procedure defines the beginning and end of the report proc-     *
!       essing. Housekeeping initializes all report values, Processing       *
!       processes the body of the report, and Wrap-Up prints the end of re-  *
!       port message and other wrap up functions.                            *
!                                                                            *
!*****************************************************************************

BEGIN-REPORT

    do HOUSEKEEPING
    do PROCESS-REPORT    
    do WRAP-UP

END-REPORT

!*****************************************************************************
!  SETUP-SECTION                                                             *
!                                                                            *
!       This section sets up common parameters including page size and       *
!       printer as well as input parameters                                  *
!                                                                            *
!*****************************************************************************
BEGIN-SETUP

    #ifdef ORACLE                       !compiler directive
        ask dbname 'Database Name'      !get connection to the db
        use {dbname}
    #endif

    #INCLUDE 'setup02c.sqc'                 
                                                    
		ask ID_EVENT                        !input parameters from the jsp	
                                   
END-SETUP

!*****************************************************************************
!                                                                            *
! HEADER                                                                     *
!                                                                            *
!       These commands will be processed every time SQR writes a page.       *
!                                                                            *
!*****************************************************************************

BEGIN-HEADING 1 

  !creates a online header           

END-HEADING

!*****************************************************************************
!                                                                            *
! FOOTER                                                                     *
!                                                                            *
!       These commands will be processed every time SQR writes a page. They  *
!       include a footer that must be in place in all pages of some reports  *
!                                                                            *
!*****************************************************************************

BEGIN-FOOTING 2

   #INCLUDE 'footer04.sqc'       ! Include file with footer information
                                 ! specific to portrait on demand report

END-FOOTING

!****************************************************************************
!                                                                           *
!   HOUSEKEEPING                                                            *
!                                                                           *
!       Set environment variables, initialize report id, initialize title   *
!       id, initialize date and time, initialize variables, and define      *
!       constants                                                           *
!                                                                           *
!       CALLED BY: BEGIN-REPORT                                             *
!       CALLS: N/A                                                          * 
!****************************************************************************

BEGIN-PROCEDURE HOUSEKEEPING

#INCLUDE 'setenv.sqc'            !Set printer environment
    
move '{ID_EVENT}'           to $id_event

!adds ' to the Client name, since ' terminates a line in SQR
let $clientName = 'Client@s Name: '
let $clientName = translate( $clientName, '@' , '''')

!adds ' to the Client name, since ' terminates a line in SQR
let $clientNumber = 'DHS Client@s Number: '
let $clientNumber = translate( $clientNumber, '@' , '''')

END-PROCEDURE HOUSEKEEPING

!****************************************************************************
!                                                                           *
!   PROCESS-REPORT                                                          *
!                                                                           *
!      This procedure includes the main processing logic of the report.     *
!                                                                           *
!       CALLED BY  : BEGIN-REPORT                                           *
!       CALLS      : GET-PERSON, GET-PERSON-LOC, PRINT-PERSONS,             *
!                    PRINT-PERSON-LOC, PRINT-SERVICE-LEVEL,  GET-MEDCAID-ID,*
! 									 GET-ATTACHMENT-1-D 																		*
!****************************************************************************

BEGIN-PROCEDURE PROCESS-REPORT 
		
		do GET-PERSON
		do GET-PERSON-LOC	
		do GET-MEDCAID-ID 
		do GET-ID-EVENT-PERSON   
		do PRINT-PERSONS
		do PRINT-PERSON-LOC
		do PRINT-SERVICE-LEVEL
		do GET-ATTACHMENT-1-D 
    
END-PROCEDURE PROCESS-REPORT                   
 
!****************************************************************************
!                                                                           *
!   GET-ATTACHMENT-1-D                                                      *
!                                                                           *
!       This procedure prints the main body of the report.                  *
!                                                                           * 
!       CALLED BY  :                                                        *
!****************************************************************************

BEGIN-PROCEDURE GET-ATTACHMENT-1-D 
alter-printer point-size=12 font=5
print 'YOUTH FOR TOMORROW'                                             (+2)    center bold
print 'INITIAL SERVICE LEVEL AUTHORIZATION'                            (+1)    center bold 
alter-printer point-size=9

print $clientName                                                      (+3,7)
print $persons                                                         (0, 15)
print $clientNumber                                                    (+1,7)
print $medcaidNumber                                                   (0,19)
print $id_person_loc                                                   (0,40)  
alter-printer point-size=12 
print 'SERVICE LEVELS'                                                 (+2,0)  bold center 
alter-printer point-size=11 font=5
print 'REQUESTED SERVICE LEVEL'                                        (+2,15) bold 
print 'AUTHORIZED SERVICE LEVEL'                                       (0,47)  bold
alter-printer point-size=10
print $rloc_basic                                                      (+1,18)
print $aloc_basic                                                      (0,50)
print $rloc_moderate                                                   (+1,18)
print $aloc_moderate                                                   (0,50)
print $rloc_specialized                                                (+1,18)
print $aloc_specialized                                                (0,50)
print $rloc_intense                                                    (+1,18)
print $aloc_intense                                                    (0,50)
alter-printer point-size=10  
print $init_care_sent_full                                             (+4,7)
print $date_received_text                                              (+2,35) underline
print $date_completed_text                                             (0,52) underline
print 'Date Received'                                                  (+1,35) 
print 'Date Completed'                                                 (0,52) 
print $healthcare_consultant_text                                      (+1,7) 
print 'Reviewer'																	                     (+1,7)
print $disclaimer_part_1                                               (+12,5)
print $disclaimer_part_2                                               (+0,5)

new-page 1

let $ploc_child = 'ploc_child(' || $ploc_child || ')'
let $ploc_type =  'ploc_type(' || $ploc_type || ')'

END-PROCEDURE GET-ATTACHMENT-1-D

!****************************************************************************
!                                                                           *
!   GET-SERVICE-LEVELS                                                      *
!                                                                           *
!   This procedure gets the service level information from the Peron_LOC    *
!   table.                                                                  *
!                                                                           * 
!       CALLED BY  : BEGIN-REPORT                                           *
!       CALLS      : N/A                                                    *
!****************************************************************************

BEGIN-PROCEDURE GET-PERSON-LOC
BEGIN-SELECT ON-ERROR=SqlErrorHandle

P_LOC.CD_PLOC_CHILD 
	    move &P_LOC.CD_PLOC_CHILD                    to $ploc_child

P_LOC.CD_PLOC_TYPE 
			move &P_LOC.CD_PLOC_TYPE                     to $ploc_type
			
P_LOC.CD_ASSCTD_RLOC
			move &P_LOC.CD_ASSCTD_RLOC                   to $assctd_rloc

P_LOC.ID_PERSON 
  		move &P_LOC.ID_PERSON                        to $id_person
  		
to_char(P_LOC.DT_PLOC_START, 'MM/DD/YYYY') &date1
   move &date1                        						 to $auth_eff_dt   

to_char(P_LOC.DT_PLOC_END, 'MM/DD/YYYY') &date2
   move &date2                        						 to $auth_exp_dt 

to_char(P_LOC.DT_SUB_TPR, 'MM/DD/YYYY') &date3
   move &date3                        						 to $date_received 

to_char(P_LOC.DT_REV_CMPLT, 'MM/DD/YYYY') &date4
   move &date4                        						 to $date_completed 

P_LOC.NM_TPR_CONS   		
			move &P_LOC.NM_TPR_CONS                      to $healthcare_consultant

FROM PERSON_LOC P_LOC
WHERE ID_PLOC_EVENT = $id_event        

END-SELECT
END-PROCEDURE PERSON-LOC

!****************************************************************************
!                                                                           *
!   GET-PERSON                                                              *
!                                                                           *
!   This procedure gets the person information from the person table.       *
!                                                                           *
!                                                                           * 
!       CALLED BY  : BEGIN-REPORT                                           *
!       CALLS      : N/A                                                    *
!****************************************************************************

BEGIN-PROCEDURE GET-PERSON 
BEGIN-SELECT ON-ERROR=SqlErrorHandle

N1.NM_NAME_FIRST &name
    move &name                                  to $persons_first

N1.NM_NAME_MIDDLE &persons_middle
    move &persons_middle                        to $persons_middle

N1.NM_NAME_LAST &persons_last
    move &persons_last                          to $persons_last

CSUFFIX2.decode &suffix                         
    move &suffix                                to $persons_suff

FROM    NAME                N1,
	      CSUFFIX2            CSUFFIX2, 
        EVENT_PERSON_LINK   EPL

WHERE   EPL.ID_EVENT        = $id_event
AND     N1.ID_PERSON        = EPL.ID_PERSON
AND     CSUFFIX2.CODE(+)    = N1.CD_NAME_SUFFIX 
AND     N1.IND_NAME_INVALID = 'N'
AND     N1.IND_NAME_PRIMARY = 'Y'
AND	    N1.DT_NAME_END_DATE = '12/31/4712'        

END-SELECT
END-PROCEDURE GET-PERSON 

!****************************************************************************
!                                                                           *
!   GET-MEDCAID-ID                                                          *
!                                                                           *
!   This procedure gets the Medcaid informaton the Peron_LOC table          *
!   table.                                                                  *
!                                                                           * 
!       CALLED BY  : BEGIN-REPORT                                           *
!       CALLS      : N/A                                                    *
!****************************************************************************

BEGIN-PROCEDURE GET-MEDCAID-ID
BEGIN-SELECT ON-ERROR=SqlErrorHandle

PID.NBR_PERSON_ID_NUMBER &medcaidNumber
    move &medcaidNumber                         to $medcaidNumber

FROM    PERSON_ID PID

WHERE   PID.CD_PERSON_ID_TYPE = 'TDHS Client #' 
AND     PID.ID_PERSON         =  $id_person

END-SELECT
END-PROCEDURE GET-MEDCAID-ID

!****************************************************************************
!                                                                           *
!   GET-ID-EVENT-PERSON                                                     *
!                                                                           *
!       CALLED BY: BEGIN-REPORT                                             *
!                                                                           *
!****************************************************************************

BEGIN-PROCEDURE GET-ID-EVENT-PERSON

BEGIN-SELECT ON-ERROR=SqlErrorHandle

ID_EVENT_PERSON &id_event_person
  move &id_event_person            to $id_event_person
  
FROM EVENT E 
WHERE ID_EVENT = $id_event

END-SELECT
END-PROCEDURE GET-ID-EVENT-PERSON

!****************************************************************************
!                                                                           *
!   PRINT-SERVICE-LEVEL                                                     *
!                                                                           *
!                                                                           *
!                                                                           * 
!       CALLED BY  : PRINT-SERVICE-LEVEL                                    *
!       CALLS      : NONE                                                   *
!****************************************************************************

BEGIN-PROCEDURE PRINT-SERVICE-LEVEL

    if $assctd_rloc = '210'
      let $rloc_basic = '[ X ] Basic'
    else 
    	let $rloc_basic = '[     ] Basic'
    end-if

    if $assctd_rloc = '220'
      let $rloc_moderate = '[ X ] Moderate'
    else 
      let $rloc_moderate = '[     ] Moderate'
    end-if

    if $assctd_rloc = '230'
      let $rloc_specialized = '[ X ] Specialized'
    else 
      let $rloc_specialized = '[     ] Specialized'
    end-if

    if $assctd_rloc = '240'
      let $rloc_intense = '[ X ] Intense'
    else 
      let $rloc_intense = '[     ] Intense'
    end-if 
		
		if $ploc_type = 'ALOC' AND $ploc_child = '210'
		  let $aloc_basic = '[ X  ] Basic'
		else 
			let $aloc_basic = '[     ] Basic'
		end-if
		
		if $ploc_type = 'ALOC' AND $ploc_child = '220'
		  let $aloc_moderate = '[ X ] Moderate'
		else 
		  let $aloc_moderate = '[     ] Moderate'
		end-if
		
		if $ploc_type = 'ALOC' AND $ploc_child = '230'
		  let $aloc_specialized = '[ X ] Specialized'
		else 
		  let $aloc_specialized = '[     ] Specialized'
		end-if
		
		if $ploc_type = 'ALOC' AND $ploc_child = '240'
		  let $aloc_intense = '[ X ] Intense'
		else 
		  let $aloc_intense = '[     ] Intense'
		end-if
		
		!let $impact_identifer = 'IMPACT Person ID: '

END-PROCEDURE PRINT-SERVICE-LEVEL

!****************************************************************************
!                                                                           *
!   PRINT-PERSONS                                                           *
!                                                                           *
!                                                                           *
!                                                                           * 
!       CALLED BY  : GET-EVENT-PERSONS                                      *
!       CALLS      : NONE                                                   *
!****************************************************************************

BEGIN-PROCEDURE PRINT-PERSONS

	if $person_suff <> '' 
	  let $pers_suff_act = ', ' || $pers_suff
	end-if
	
	if $persons_middle = ''
	  let $persons = $persons_first || ' ' || $persons_last || $pers_suff_act
	else
	  let $persons = $persons_first || ' ' || $persons_middle || ' ' || $persons_last || $pers_suff_act
	end-if
                      
END-PROCEDURE PRINT-PERSONS   

!****************************************************************************
!                                                                           *
!   PRINT-PERSON-LOC                                                        *
!                                                                           *
!                                                                           *
!                                                                           * 
!       CALLED BY  :                                                        *
!       CALLS      :                                                        *
!****************************************************************************

BEGIN-PROCEDURE PRINT-PERSON-LOC

!builds the person care text
	if $id_person = '' 
	  let $id_person_loc = 'IMPACT Person ID:' 
	else
	  let $id_person_loc = 'IMPACT Person ID: ' || $id_person
	end-if	
	
!builds the initial care text
	if $auth_eff_dt = '01/01/1850' OR $auth_eff_dt = ''
	  let $init_care_sent_part_1 = 'Initial Service Level is authorized effective ________ ' 
	else
		let $init_care_sent_part_1 = 'Initial Service Level is authorized effective ' || $auth_eff_dt
	end-if
	
	if $auth_exp_dt  = '12/31/4712' OR $auth_exp_dt = '' OR $auth_exp_dt  = '12/31/3500'
		  let $init_care_sent_part_2 = ' and the expiration date is __________.' 
		else
			let $init_care_sent_part_2 = ' and the expiration date is ' || $auth_exp_dt || '.'
	end-if
	
	let $init_care_sent_full = $init_care_sent_part_1 || $init_care_sent_part_2
		
!builds the date received text
	if $date_received = ''
	  let $date_received_text = '                        '
	else  
	  let $date_received_text = '    ' || $date_received || '   '     
	end-if  
	
!builds the date completed text
	if $date_completed = ''
	  let $date_completed_text = '                       '     
	else
	  let $date_completed_text = '   ' || $date_completed || '     '    
	end-if
	
	if $healthcare_consultant = ''
	  let $healthcare_consultant_text = '_______________, Clinical Healthcare Consultant' 
	else
		let $healthcare_consultant_text = $healthcare_consultant  || ', Clinical Healthcare Consultant' 
	end-if
																												
	if strtodate($auth_eff_dt,'mm/dd/yyyy') <= strtodate('2/28/2005','mm/dd/yyyy') 
     OR $id_event_person != '999999997'
		let $disclaimer_part_1 = 'This recently enhanced system will produce complete Initial Authorization reports and complete Utilization Review'
		let $disclaimer_part_2 = 'reports only for YFT reports completed on or after February 28, 2005.'
	else 
	 	let $disclaimer = ''
  end-if	
		
END-PROCEDURE PRINT-PERSON-LOC  

!****************************************************************************
!                                                                           *
!   WRAP-UP                                                                 *
!                                                                           *
!       This procedure prints the messages for the end of the report as well*
!       as over all totals.                                                 *
!                                                                           * 
!       CALLED BY  : BEGIN-REPORT                                           *
!       CALLS      : NONE                                                   *
!****************************************************************************

BEGIN-PROCEDURE WRAP-UP

   !NEW-PAGE

END-PROCEDURE WRAP-UP


!***********************************************************************
!                                                                      *
! SqlErrorHandle                                                       *
!                                                                      *
!      This section handles formatting of messages for SQL errors.     *
!      When an SQL error is encounterd, the program will write to      *   
!      the log file, program processing does not stop for SQL errors.  *
!                                                                      *
!  CALLS:  None                                                        *
!                                                                      *
!***********************************************************************

BEGIN-PROCEDURE SqlErrorHandle 

  print 'SQL error - Processing Terminated'               (+1,2)
  print $error                                            (+1,2)
  print 'ERROR: SQL error halted loader processing'       (+0)
  print $error                                            (+1,2)
  print 'SQL Error: '                                     (+0)
  print $sql-error                                        (+0)
  print $error                                            (+1,2)
  print 'SQL Status: '                                    (+0)
  print #sql-status                                       (+0)

  STOP

END-PROCEDURE SqlErrorHandle


!****************************************************************************
!                                                                           *
!                                INCLUDE SECTION                            *
!                                                                           *
!   Include all files that contain common modules or procedures that        *
!   will be used in the report                                              *
!****************************************************************************


#INCLUDE 'cfpstamp.sqc'
