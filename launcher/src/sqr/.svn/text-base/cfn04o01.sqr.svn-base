!*******************************************************************************
!  PreBill for Delivered Services Batch Report                                 *
!*******************************************************************************

!*******************************************************************************
!                                                                              *
! PROGRAM NAME:         cfn04o01.sqr                                           *
!                                                                              *
! PROGRAM LOCATION:     /caps/users/mccleabg/work/sqr                          *
!                                                                              *
!                                                                              *
! DATE:                 11/29/95                                               *
!                                                                              *
! PROGRAMMER NAME:      Brad McCleary                                          *
!                                                                              *
! PROGRAM DESCRIPTION:  This PreBills batch report will be generated monthly.  *
!                       The files will be broken down by region and will be    *
!                       launched by their respective Contract Managers.  The   *
!                       actual PreBills will be produced after an extract and  *
!                       update process that will fill in the missing field     *
!                       values.                                                *
!                                                                              *
!*******************************************************************************
!*******************************************************************************
!                                                                              *
! PROGRAM MODIFICATION SECTION                                                 *
! ----------------------------                                                 *
!                                                                              *
!   DATE     PROGRAMMER                       MODIFICATION                     *
! --------   ----------   -------------------------------------------------    *
! 11/29/95   B.McCleary   Initial Programming                                  *
! 03/07/96   B.McCleary   SIR 3364 - Change input parameters to only include   *
!                         region.  Make the address select by id rsrc addr.    *
!                         Make a NEW-REPORT to name the output file            *
!                         dynamically.                                         *
! 04/04/96   B.McCleary   SIR 20033 - Added new input parameter to redirect    *
!                         the location of the output file.                     *
! 04/09/96   B.McCleary   SIR 20191 - Changed the year/mo header output.       *
! 04/11/96   B.McCleary   SIR 20365 - Added flags to prevent the first page    *
!                         from printing blank.                                 *
! 04/15/96   B.McCleary   SIR 20381 - Changed order of SQL calls and added     *
!                         logic to keep appropriate headers with their         *
!                         respective contracts.                                *
! 04/15/96   B.McCleary   SIR 20385 - Changed the ORDER BY to sort by last     *
!                         then first name.                                     *
! 04/16/96   B.McCleary   SIR 3940 - Added SQR to make the report print in     *
!                         PCL format; created unique layout to be used by all  *
!                         landscape batch reports printing in PCL.             *
! 04/16/96   B.McCleary   SIR 20472 - Added flag so the contract manager does  *
!                         not print when there are no detail line items.       *
! 04/24/96   B.McCleary   SIR 20634 - Changed placement of header info SQL's   *
!                         and removed code that page breaks by contract;       *
!                         this version NEW-PAGES every time a new invoice is   *
!                         selected.                                            *
! 04/30/96   B. McCleary  SIR 20201 - Added id svc auth dtl and svc auth dtl   *
!                         begin and term dates to the code.                    *
! 05/06/96   B. McCleary  SIR 20926 - Added primary name condition and outer   *
!                         joins to svc auth dtl SQL.                           *
! 05/22/96   B. McCleary  SIR 21390 - Added condition to get only primary      *
!                         names in the delvrd svc dtl SQL; added an invoice    *
!                         counter to make sure the report NEW-PAGES when the   *
!                         invoice changes within a contract.                   *
! 07/24/96   K.Cys        SIR 10400 - Added embedded SQL to GET-CONTRACT       *
!                         -MANAGER so that only the contract manager for       *
!                         Delivered Services Invoices will be selected.        *
! 07/25/96   K.Cys        SIR 21838 - Added condition to GET-CONTRACT-MANAGER  *
!                         SQL to select only the latest and primary occurence  *
!                         of a name.                                           *
! 08/01/96   K.Cys        SIR 21721 - ENHANCEMENT:  Client does not want       *
!                         rate to print on report.  Left code commented out    *
!                         in case they change their minds later.               *
! 08/07/96   K.Cys        SIR 21781 - ENHANCEMENT:  Client wants Region #      *
!                         to print in Report Header.  Added code to do so.     *
! 08/22/96   K.Cys        SIR 22059 - Contract Manager name was not matching   *
!                         CM address and invoices in a given contract.         *
!                         Removed logic that selects contract manager name     *
!                         in GET-CONTRACT-MANAGER and created a new procedure  *
!                         called GET-CM-NAME. This procedure is called from    *
!                         GET-INVOICE along with all the address linking       *
!                         procudures.  This should keep the contract manager   *
!                         name in synch with the proper invoice and address    *
!                         information.                                         *
! 10/12/96   K.Cys        SIR 11820 - PERF:  Changed name max last update      *
!                         linking in GET-CM-NAME to link of contract manager   *
!                         id.  Divided GET-SVC-DTL into two procedures.  GET   *
!                         -DTL-NAME handles retrieving client name info. for   *
!                         a given detail line item.  These changes address     *
!                         the lengthy run time of this report in Production.   *
!                         Previously, full table scans were being done on      *
!                         CONTRACT and NAME.                                   *
! 10/27/2003 dejuanr      SIR 19813: Added alter-printer font=300 point-size=14*
!                                    This will bold the font and make it       *
!                                    larger.                                   *
!                                                                              *
! SIR 23172            Modified code related to the NAME table.  The outer     *
! hallrv               join between DELVRD_SVC_DTL and NAME in GET-SVC-DTL     *
! 10/20/2004           failed to use the (+) on two of four NAME columns,      *
!                      resulting in failure of the SQL to act as an outer      *
!                      join.  Rows without associated, valid names were left   *
!                      off the report.                                         *
!                                                                              *
!                      Further, even though the NAME table is included in the  *
!                      SQL in GET-SVC-DTL, the procedure also called a         *
!                      separate procedure, GET-DTL-NAME, to retrieve NAME      *
!                      again.  This was, supposedly, to avoid a full table     *
!                      scan of DELVRD_SVC_DTL, but simply adding this call     *
!                      does not provide that benefit.  Removed this call.      *
!                                                                              *
!                      Modified the code so that client and contract manager   *
!                      names are acquired from the PERSON table instead of the *
!                      NAME table.  Code is consistent with changes also made  *
!                      to the single-print prebill report program.             *
!                                                                              *
!                      Even though CLIENT names are retrieved, the procedure   *
!                      used variable names of $emp_first and $emp_last.        *
!                      Replaced these variable names with $client_nm_first and *
!                      $client_nm_last to avoid confusion.                     *
!                                                                              *
!                      Changed other variable names that were misleading and   *
!                      did not represent their content.                        *
!*******************************************************************************

!*******************************************************************************
!                                                                              *
! REPORT PROCESSING SECTION                                                    *
!                                                                              *
!       This procedure defines the beginning and end of the report proc-       *
!       essing. Housekeeping initializes all report values, Processing         *
!       processes the body of the report, and Wrap-Up prints the end of report *
!       message and other wrap up functions.                                   *
!                                                                              *
!*******************************************************************************

BEGIN-REPORT

    do HOUSEKEEPING

    do PROCESS-REPORT

    do WRAP-UP

END-REPORT

!*******************************************************************************
!  SETUP-SECTION                                                               *
!                                                                              *
!       This section sets up common parameters including page size and         *
!       printer as well as input parameters                                    *
!                                                                              *
!*******************************************************************************

BEGIN-SETUP

!*******SIR 3940*******
!******* This section sets the report to direct print in PCL format *******

!   #DEFINE ColR 108

!   DECLARE-REPORT R1
!   PRINTER-TYPE=HPLASERJET
!   END-DECLARE

!   DECLARE-LAYOUT DEFAULT
!   ORIENTATION=LANDSCAPE
!   CHAR-WIDTH=1
!   LEFT-MARGIN=.21
!   RIGHT-MARGIN=.21
!   TOP-MARGIN=.17
!   BOTTOM-MARGIN=.50
!   END-DECLARE

    #INCLUDE 'setup01c.sqc'                 ! control codes for landscape
                                                ! on batch reports


    #ifdef ORACLE                           !compiler directive
        ask dbname 'Database Name'
        use {dbname}
    #endif


    ask CD_CNTRCT_REGION                    !SIR 3364 - Reduces parameters to
                                            !region only
!***SIR 20033***

    ask FILE_PATH                           !new path to put the output file in
                                            !another directory
END-SETUP

!*******************************************************************************
!                                                                              *
! HEADER                                                                       *
!                                                                              *
!       These commands will be processed every time SQR writes a page. They    *
!       include the report title, date, time, page number, report id, and      *
!       column headers. XXX lines are reserved to print the heading at the     *
!       top of each page. This includes blank lines. A common setup header     *
!       has been created called XXXXXXXXXXXXXXXXXXX for landscape or portrait  *
!       reports.                                                               *
!                                                                              *
!*******************************************************************************

BEGIN-HEADING 17

    let $region = 'REGION ' || $input_region  !***SIR 21781***

    move 'Pre-Bill for Delivered Services'   to $ReportTitle
    move 'cfn04o01'                          to $ReportID
    move $region                             to $ReportSubHeading !***SIR 21781*

    #INCLUDE 'header05.sqc'     ! Include file with header information

    print 'PROVIDER NAME AND ADDRESS: '         (+1,1)
    print 'VENDOR ID NUMBER: '                  (+0,98)

  if $last_vendor <> ''
    print $last_vendor                          (+0,118) edit B9999999999999
  else
    print $vendor_id                            (+0,118) edit B9999999999999
  end-if

    print 'CONTRACT NUMBER : '                  (+1,98)
    print #last_contract                        (+0,116) edit B999999999999999
    print $prov_name                            (+1,1)
    print 'INVOICE NUMBER  : '                  (+0,98)
    print #last_invoice                         (+0,116) edit B999999999999999

    print $prov_addr_ln1                        (+1,1)

    !***SIR 20191***
    print 'INVOICE MO/YEAR : '                  (+0,98)

  if #last_mo <> 0
    print #last_mo                              (+0,125) edit 00
    print '/'                                   ()
    print #last_yr                              ()       edit 9999
  else

    if #svc_mo <> 0
      print #svc_mo                             (+0,125) edit 00
      print '/'                                 ()
      print #svc_yr                             ()       edit 9999
    end-if

  end-if

    print $prov_addr_ln2                        (+1,1)

    print $prov_city                            (+1,1)

if $prov_city <> ''
    print ','                                   ()
    print $prov_state                           ()
    print ' '                                   ()
end-if

    print $prov_zip                             ()

    print 'RETURN TO CONTRACT MANAGER: '        (+2,98)


!*******SIR 20472*******

    if $print_flag = 'Y'
      print $cm_name                            (+1,98)
    end-if

    print $cm_addr_ln1                          (+1,98)

    print $cm_addr_ln2                          (+1,98)

    print $cm_city                              (+1,98)

if $cm_city <> ''
    print ','                                   ()
end-if

if #last_contract <> 0
    print 'TX'                                  ()
    print ' '                                   ()
end-if

    print $cm_zip                               ()

print 'LIN'                                     (+2,1)
print 'CLIENT'                                  (+0,43)
print 'SERV'                                    (+0,66)
print 'CSL'                                     (+0,127)
print 'NO.'                                     (+1,1)
print 'LAST NAME'                               (+0,5)
print 'FIRST NAME'                              (+0,29)
print 'NUMBER'                                  (+0,43)
print 'SER'                                     (+0,54)
print 'TYP'                                     (+0,59)
print 'MM/YYYY'                                 (+0,64)
print 'RATE'                                    (+0,80)
print 'QUANTITY'                                (+0,86)
print 'FEE PAID'                                (+0,96)
print 'AMOUNT'                                  (+0,108)
print 'NO.'                                     (+0,127)

!*******SIR 20201*******
print 'SVC AUTH DTL ID'                         (+0,132)
print 'BEGIN'                                   (+0,149)
print 'TERM'                                    (+0,161)
print '-'                                       (+1,1,3)    fill
print '-'                                       (+0,5,22)   fill
print '-'                                       (+0,29,12)  fill
print '-'                                       (+0,43,10)  fill
print '-'                                       (+0,54,3)   fill
print '-'                                       (+0,59,3)   fill
print '-'                                       (+0,64,7)   fill
print '-'                                       (+0,72,12)  fill
print '-'                                       (+0,86,8)   fill
print '-'                                       (+0,96,10)  fill
print '-'                                       (+0,108,17) fill
print '-'                                       (+0,127,3)  fill
print '-'                                       (+0,132,15) fill
print '-'                                       (+0,149,10) fill
print '-'                                       (+0,161,10) fill

END-HEADING

!*******************************************************************************
!                                                                              *
!   HOUSEKEEPING                                                               *
!                                                                              *
!       Set environment variables, initialize report id, initialize title      *
!       id, initialize date and time, initialize variables, and define         *
!       constants                                                              *
!                                                                              *
!       CALLED BY: BEGIN-REPORT                                                *
!                                                                              *
!*******************************************************************************

BEGIN-PROCEDURE HOUSEKEEPING

   alter-printer font=300 point-size=14

   #INCLUDE 'setenv.sqc'            !Set printer environment

    move '{CD_CNTRCT_REGION}'       to $input_region

    move '{FILE_PATH}'              to $file_path

    move 'STOP'                     to $STOP
    move 'N'                        to $new_page_flag

    move 'PRB'                      to $PRB
    move 'DUR'                      to $DUR
    move 'DCR'                      to $DCR
    move 'DSB'                      to $DSB
    move 'No PERSON Last Name'      to $UNKNOWN_LAST_NM                !SIR 23172
    move 'No First Nm'              to $UNKNOWN_FRST_NM                !SIR 23172

   let $report_name = $file_path || '/DS' || $input_region || '.lis'
   NEW-REPORT $report_name

END-PROCEDURE HOUSEKEEPING

!*******************************************************************************
!                                                                              *
!   PROCESS-REPORT                                                             *
!                                                                              *
!      This procedure includes the main processing logic of the report.        *
!                                                                              *
!       CALLED BY  : BEGIN-REPORT                                              *
!       CALLS      : GET-CONTRACT-MGR, PRINT-TOTALS                            *
!*******************************************************************************

BEGIN-PROCEDURE PROCESS-REPORT

!*******SIR 3940*******

!  USE-REPORT R1
!  ALTER-PRINTER FONT=0 PITCH=16.67

    do GET-CONTRACT-MGR
    do PRINT-TOTALS

END-PROCEDURE PROCESS-REPORT

!*******************************************************************************
!                                                                              *
!   GET-CONTRACT-MGR                                                           *
!                                                                              *
!       This procedure uses the input region to retrieve the appropriate       *
!       contract and contract manager id.                                      *
!                                                                              *
!       CALLED BY  : PROCESS-REPORT                                            *
!       CALLS      : GET-INVOICE                                               *
!*******************************************************************************

BEGIN-PROCEDURE GET-CONTRACT-MGR

    move 0                        to #count1

BEGIN-SELECT ON-ERROR=SqlErrorHandle

C.ID_CONTRACT
    move &C.ID_CONTRACT                             to #id_contract

C.ID_RESOURCE
    move &C.ID_RESOURCE                             to #id_resource

C.ID_RSRC_ADDRESS
    move &C.ID_RSRC_ADDRESS                         to #id_resource_addr

C.ID_CNTRCT_MANAGER
    move &C.ID_CNTRCT_MANAGER                       to #id_cm


    let #count1 = #count1 + 1                    !******************************
                                                 !SIR 22059 Code to retrieve   *
                                                 !CM name is now in GET-CM-NAME*
                                                 !******************************
    do GET-INVOICE


!***SIR 20365***

    if (#count2 = 0) and ($stop_flag <> $STOP)
        move 'N' to $new_page_flag
    else
        move 'Y' to $new_page_flag
    end-if

FROM    CONTRACT    C

WHERE   C.CD_CNTRCT_REGION = $input_region
AND     C.ID_CONTRACT IN (SELECT INV.ID_CONTRACT !***SIR 10400***
                          FROM INVOICE INV
                          WHERE INV.CD_INVO_PHASE = $PRB
                          AND (INV.CD_INVO_TYPE = $DSB
                           OR  INV.CD_INVO_TYPE = $DUR
                           OR  INV.CD_INVO_TYPE = $DCR))

END-SELECT

END-PROCEDURE GET-CONTRACT-MGR

!*******************************************************************************
!                                                                              *
!   GET-INVOICE                                                                *
!                                                                              *
!       This procedure will select the invoice from the contract passed.       *
!       It also calls another procedure that will drive off the invoice to     *
!       select the detail rows for the report.  Then, it calls a procedure     *
!       to print the subtotal and signature lines for the invoice selected.    *
!                                                                              *
!       CALLED BY  : GET-CONTRACT-MGR                                          *
!       CALLS      : GET-SVC-DTL                                               *
!*******************************************************************************

BEGIN-PROCEDURE GET-INVOICE

  move 0                    to #invo_count

BEGIN-SELECT ON-ERROR=SqlErrorHandle

I.ID_INVOICE
    move &I.ID_INVOICE                          to #id_invoice

I.YR_INVO_YEAR
    move &I.YR_INVO_YEAR                        to #svc_yr

I.MO_INVO_MONTH
    move &I.MO_INVO_MONTH                       to #svc_mo

I.NBR_INVO_VID
    move &I.NBR_INVO_VID                        to $vendor_id


  move 0                                        to #count2

!*******SIR 21390*******
  let #invo_count = #invo_count + 1

  if #invo_count > 1
      move 'Y'                  to $new_page_flag
  end-if

!SIR 20381 - These SQL's are called only after the information for the last
!            invoice has been printed in the header when there is a NEW-PAGE.
!            This prevents the header info for the NEXT contract from being
!            printed on the page with the current invoice.
!SIR 20634 - Moved SQL's to this call in order to page break by invoice and
!            keep the appropriate header info together.

  if $new_page_flag = 'N'
      do GET-CM-NAME  !***SIR 22059***
      do GET-RESOURCE
      do GET-PROV-ADDR
      do GET-EMPLOYEE
      do GET-OFFICE
      do GET-CM-ADDR
  else
      do PRINT-TOTALS
      NEW-PAGE
      do GET-CM-NAME !***SIR 22059***
      do GET-RESOURCE
      do GET-PROV-ADDR
      do GET-EMPLOYEE
      do GET-OFFICE
      do GET-CM-ADDR
  end-if

    do GET-SVC-DTL

FROM    INVOICE     I

WHERE   I.ID_CONTRACT   = #id_contract
AND     I.CD_INVO_PHASE = $PRB
AND     I.CD_INVO_TYPE IN ($DUR, $DCR, $DSB)

END-SELECT

END-PROCEDURE GET-INVOICE

!**********SIR 22059************************************************************
!                                                                              *
!  GET-CM-NAME                                                                 *
!       This procedure will get the contract manager name for the contract     *
!       passed to GET-INVOICE by GET-CONTRACT-MANAGER.                         *
!                                                                              *
!   CALLED BY:  GET-INVOICE                                                    *
!       CALLS:  NONE                                                           *
!*******************************************************************************

BEGIN-PROCEDURE GET-CM-NAME

BEGIN-SELECT ON-ERROR=SqlErrorHandle
                                                                               !SIR 23172: Changed this procedure to
                                                                               !           acquire contract manager name
                                                                               !           from PERSON instead of NAME.
P.NM_PERSON_FIRST || ' ' || P.NM_PERSON_MIDDLE || ' ' || P.NM_PERSON_LAST  &cm_name
    move &cm_name                                   to $cm_name

FROM    PERSON P

WHERE   P.ID_PERSON = #id_cm

END-SELECT

END-PROCEDURE GET-CM-NAME

!*******************************************************************************
!                                                                              *
!   GET-RESOURCE                                                               *
!                                                                              *
!       This procedure will select the provider name for the header.           *
!                                                                              *
!       CALLED BY  : PRINT-SVC-DTL                                             *
!       CALLS      : NONE                                                      *
!*******************************************************************************

BEGIN-PROCEDURE GET-RESOURCE

BEGIN-SELECT ON-ERROR=SqlErrorHandle

A.NM_RESOURCE
    move &A.NM_RESOURCE                     to $prov_name

FROM    CAPS_RESOURCE   A

WHERE   A.ID_RESOURCE = #id_resource

END-SELECT

END-PROCEDURE GET-RESOURCE

!*******************************************************************************
!                                                                              *
!   GET-PROV-ADDR                                                              *
!                                                                              *
!       This procedure will select provider address information for the        *
!       header.                                                                *
!                                                                              *
!                                                                              *
!       CALLED BY  : PRINT-SVC-DTL                                             *
!       CALLS      : NONE                                                      *
!*******************************************************************************

BEGIN-PROCEDURE GET-PROV-ADDR

BEGIN-SELECT ON-ERROR=SqlErrorHandle

A.ADDR_RSRC_ADDR_ST_LN_1
    move &A.ADDR_RSRC_ADDR_ST_LN_1              to $prov_addr_ln1

A.ADDR_RSRC_ADDR_ST_LN_2
    move &A.ADDR_RSRC_ADDR_ST_LN_2              to $prov_addr_ln2

A.ADDR_RSRC_ADDR_CITY
    move &A.ADDR_RSRC_ADDR_CITY                 to $prov_city

A.CD_RSRC_ADDR_STATE
    move &A.CD_RSRC_ADDR_STATE                  to $prov_state

A.ADDR_RSRC_ADDR_ZIP
    move &A.ADDR_RSRC_ADDR_ZIP                  to $prov_zip

FROM    RESOURCE_ADDRESS    A

WHERE   A.ID_RSRC_ADDRESS = #id_resource_addr  !SIR 3364 - use id rsrc addr
                                               !instead of id rsrc
END-SELECT

END-PROCEDURE GET-PROV-ADDR

!*******************************************************************************
!                                                                              *
!   GET-EMPLOYEE                                                               *
!                                                                              *
!       This procedure will select the employee office id so we can retrieve   *
!       contract manager's mail code and, eventually, the office address.      *
!                                                                              *
!       CALLED BY  : PRINT-SVC-DTL                                             *
!       CALLS      : NONE                                                      *
!*******************************************************************************

BEGIN-PROCEDURE GET-EMPLOYEE

BEGIN-SELECT ON-ERROR=SqlErrorHandle

E.ID_EMP_OFFICE
    move &E.ID_EMP_OFFICE                         to #id_emp_office            !SIR 23172: Changed variable nm
                                                                               !           (from #id_employee)
FROM    EMPLOYEE    E

WHERE   E.ID_PERSON = #id_cm

END-SELECT

END-PROCEDURE GET-EMPLOYEE

!*******************************************************************************
!                                                                              *
!   GET-OFFICE                                                                 *
!                                                                              *
!       This procedure will select the office code so we can retrieve the      *
!       mail code information.                                                 *
!                                                                              *
!       CALLED BY  : PRINT-SVC-DTL                                             *
!       CALLS      : NONE                                                      *
!*******************************************************************************

BEGIN-PROCEDURE GET-OFFICE

BEGIN-SELECT ON-ERROR=SqlErrorHandle

O.CD_OFFICE_MAIL
    move &O.CD_OFFICE_MAIL                      to $office_mail_cd             !SIR 23172: Changed variable nm
                                                                               !           (from $office_code)
FROM    OFFICE      O

WHERE   O.ID_OFFICE = #id_emp_office

END-SELECT

END-PROCEDURE GET-OFFICE

!*******************************************************************************
!                                                                              *
!   GET-CM-ADDR                                                                *
!                                                                              *
!       This procedure will select the contract manager's address given the    *
!       office code.                                                           *
!                                                                              *
!                                                                              *
!       CALLED BY  : PRINT-SVC-DTL                                             *
!       CALLS      : NONE                                                      *
!*******************************************************************************

BEGIN-PROCEDURE GET-CM-ADDR

BEGIN-SELECT ON-ERROR=SqlErrorHandle

M.ADDR_MAIL_CODE_ST_LN_1
    move &M.ADDR_MAIL_CODE_ST_LN_1              to $cm_addr_ln1

M.ADDR_MAIL_CODE_ST_LN_2
    move &M.ADDR_MAIL_CODE_ST_LN_2              to $cm_addr_ln2

M.ADDR_MAIL_CODE_CITY
    move &M.ADDR_MAIL_CODE_CITY                 to $cm_city

M.ADDR_MAIL_CODE_ZIP
    move &M.ADDR_MAIL_CODE_ZIP                  to $cm_zip


FROM    MAIL_CODE    M

WHERE   M.CD_MAIL_CODE = $office_mail_cd

END-SELECT

END-PROCEDURE GET-CM-ADDR

!*******************************************************************************
!                                                                              *
!   GET-SVC-DTL                                                                *
!                                                                              *
!       This procedure will select the report rows for each individual         *
!       contract.                                                              *
!                                                                              *
!       CALLED BY  : GET-INVOICE                                               *
!       CALLS      : PRINT-SVC-DTL                                             *
!*******************************************************************************

BEGIN-PROCEDURE GET-SVC-DTL

BEGIN-SELECT ON-ERROR=SqlErrorHandle

!*******SIR 20201*******

SAD.ID_SVC_AUTH_DTL
    move &SAD.ID_SVC_AUTH_DTL                  to #id_svc_auth_dtl

to_char(SAD.DT_SVC_AUTH_DTL_BEGIN, 'MM/DD/YYYY') &svc_begin
    move &svc_begin                            to $dt_svc_begin

to_char(SAD.DT_SVC_AUTH_DTL_TERM, 'MM/DD/YYYY') &svc_term
    move &svc_term                             to $dt_svc_term

DSD.ID_SVC_DTL_PERSON
    move &DSD.ID_SVC_DTL_PERSON                to #id_person

DSD.CD_SVC_DTL_SERVICE
    move &DSD.CD_SVC_DTL_SERVICE               to $ser

DSD.CD_SVC_DTL_UNIT_TYPE
    move &DSD.CD_SVC_DTL_UNIT_TYPE             to $unit

DSD.MO_SVC_DTL_SVC_MONTH
    move &DSD.MO_SVC_DTL_SVC_MONTH             to #svc_dtl_mo

DSD.YR_SVC_DTL_SVC_YEAR
    move &DSD.YR_SVC_DTL_SVC_YEAR              to #svc_dtl_yr

DSD.AMT_SVC_DTL_UNIT_RATE
    move &DSD.AMT_SVC_DTL_UNIT_RATE            to #rate

DSD.NBR_SVC_DTL_CSLI
    move &DSD.NBR_SVC_DTL_CSLI                 to #csli

P.NM_PERSON_FIRST                              !***SIR 11820***
    move &P.NM_PERSON_FIRST                    to $client_nm_first   !SIR 23172: Changed variable nm and source

P.NM_PERSON_LAST                               !***SIR 11820***
    move &P.NM_PERSON_LAST                     to $client_nm_last    !SIR 23172: Changed variable nm and source

    let #count2 = #count2 + 1

    !do GET-DTL-NAME !***SIR 11820*** note:  name is retrieved in this section  !SIR 23172: Commented code
                     !to order the detail line items.  GET-DTL-NAME actually
                     !retrieves the printed name.
    do PRINT-SVC-DTL

FROM    PERSON                  P,                                     !SIR 23172: Changed from NAME to PERSON table
        SVC_AUTH_DETAIL         SAD,
        DELVRD_SVC_DTL          DSD

WHERE   P.ID_PERSON            = DSD.ID_SVC_DTL_PERSON   !SIR 20926 - added outer join... SIR 23172: Changed to PERSON table and
AND     SAD.ID_SVC_AUTH_DTL    = DSD.ID_SVC_AUTH_DTL                                      !          removed outer join
AND     DSD.ID_INVOICE         = #id_invoice

!*******SIR 20385******* !***SIR 11820***
ORDER BY P.NM_PERSON_LAST, P.NM_PERSON_FIRST                           !SIR 23172: Changed from NAME columns to PERSON columns

END-SELECT

END-PROCEDURE GET-SVC-DTL

!*******************************************************************************
!                         ***SIR 11820***                                      *
!   GET-DTL-NAME                                                               *
!                                                                              *
!       This procedure addresses a performance problem.  Name is               *
!       retrieved separately to prevent full table scans on DELVD_SVC_DTL.     *
!                                                                              *
!       CALLED BY:  GET-SVC-DTL                                                *
!       CALLS    :  NONE                                                       *
!*******************************************************************************
!BEGIN-PROCEDURE GET-DTL-NAME

!BEGIN-SELECT ON-ERROR=SqlErrorHandle

!NM.NM_NAME_FIRST
!    move &NM.NM_NAME_FIRST                      to $emp_first

!NM.NM_NAME_LAST
!    move &NM.NM_NAME_LAST                       to $emp_last

!FROM    NAME                    NM

!WHERE   NM.ID_PERSON(+) = #id_person  !SIR 20926 - added outer join
!AND     NM.IND_NAME_PRIMARY(+) = 'Y'  !SIR 20926 - added outer join
                                                  !and condition
!*******SIR 21390 *******
!AND NM.DT_NAME_END_DATE = (SELECT   MAX(NM2.DT_NAME_END_DATE)
!                           FROM    NAME        NM2,
!                                   DELVRD_SVC_DTL  D2
!                           WHERE NM2.ID_PERSON = #id_person
!                           AND   NM2.ID_PERSON = D2.ID_SVC_DTL_PERSON)

!END-SELECT

!END-PROCEDURE GET-DTL-NAME

!*******************************************************************************
!                                                                              *
!   PRINT-SVC-DTL                                                              *
!                                                                              *
!       This procedure prints the report rows for each invoice.                *
!                                                                              *
!       CALLED BY  : GET-SVC-DTL                                               *
!       CALLS      : GET-RESOURCE, GET-PROV-ADDR, GET-EMPLOYEE, GET-OFFICE,    *
!                    GET-CM-ADDR                                               *
!*******************************************************************************

BEGIN-PROCEDURE PRINT-SVC-DTL

  move 'Y'                              to $print_flag

!***SIR 20365***

  move $STOP                            to $stop_flag


!SIR 20381 - The host variables are moved to "last" variables only when svc dtl
!            rows exist for the contract.  This makes sure the variables are not
!            overwritten by other contracts with no svc dtl records.


      move #id_contract                 to #last_contract
      move #id_invoice                  to #last_invoice
      move $vendor_id                   to $last_vendor
      move #svc_yr                      to #last_yr
      move #svc_mo                      to #last_mo


print #count2                                   (+2,1)   edit 888

let $client_nm_last = nvl($client_nm_last,$UNKNOWN_LAST_NM)            !SIR 23172
let $client_nm_first = nvl($client_nm_first,$UNKNOWN_FRST_NM)          !SIR 23172

print $client_nm_last                           (+0,5)                 !SIR 23172
print $client_nm_first                          (+0,29)                !SIR 23172
print #id_person                                (+0,43)  edit 888888888
print $ser                                      (+0,54)
print $unit                                     (+0,59)

if #svc_dtl_yr <> 0
print #svc_dtl_mo                               (+0,64)  edit 00
print '/'                                         ()
print #svc_dtl_yr                                 ()     edit 8888
end-if

!if #rate <> 0        !***SIR 21721***
    !print #rate                                 (+0,72) edit $$$$$$$$$.00
!end-if

print #csli                                     (+0,127) edit B9

!*******SIR 20201*******
print #id_svc_auth_dtl                  (+0,132) edit 8888888888888888
print $dt_svc_begin                     (+0,149)
print $dt_svc_term                      (+0,161)

END-PROCEDURE PRINT-SVC-DTL

!*******************************************************************************
!                                                                              *
!   PRINT-TOTALS                                                               *
!                                                                              *
!       This procedure prints the subtotals and signature lines at the end     *
!       of each invoice selected.                                              *
!                                                                              *
!       CALLED BY  : PROCESS-REPORT, PRINT-SVC-DTL                             *
!       CALLS      : NONE                                                      *
!*******************************************************************************

BEGIN-PROCEDURE PRINT-TOTALS

next-listing need = 18

print 'Pre-Bill Subtotal: '                     (+3,63)
print '_'                                       (+0,95,30) fill
print 'Attached Form(s) 2016 Subtotal: '        (+3,50)
print '_'                                       (+0,95,30) fill
print 'Total Claimed Amount: '                  (+3,60)
print '_'                                       (+0,95,30) fill

print 'NOTE: CLAIMING FOR SERVICES NOT ACTUALLY PROVIDED CONSTITUTES FRAUD' (+3,1)

print '_'                                       (+4,75,30)  fill
print '_'                                       (+0,110,20) fill
print 'Provider Signature'                      (+1,75)
print 'Date'                                    (+0,110)

END-PROCEDURE PRINT-TOTALS

!*******************************************************************************
!                                                                              *
!   WRAP-UP                                                                    *
!                                                                              *
!       This procedure prints the messages for the end of the report as well   *
!       as over all totals.                                                    *
!                                                                              *
!       CALLED BY  : BEGIN-REPORT                                              *
!       CALLS      : NONE                                                      *
!*******************************************************************************

BEGIN-PROCEDURE WRAP-UP

   NEW-PAGE

END-PROCEDURE WRAP-UP


!*******************************************************************************
!                                                                              *
! SqlErrorHandle                                                               *
!                                                                              *
!      This section handles formatting of messages for SQL errors.             *
!      When an SQL error is encounterd, the program will write to              *
!      the log file, program processing does not stop for SQL errors.          *
!                                                                              *
!  CALLS:  None                                                                *
!                                                                              *
!*******************************************************************************

BEGIN-PROCEDURE SqlErrorHandle


  print 'SQL error - Processing Terminated'               (+1,2)
  print $error                                            (+1,2)
  print 'ERROR: SQL error halted loader processing'       (+0)
  print $error                                            (+1,2)
  print 'SQL Error: '                                     (+0)
  print $sql-error                                        (+0)
  print $error                                            (+1,2)
  print 'SQL Status: '                                    (+0)
  print #sql-status                                       (+0)
  STOP

END-PROCEDURE SqlErrorHandle


!*******************************************************************************
!                                                                              *
!                                INCLUDE SECTION                               *
!                                                                              *
!   Include all files that contain common modules or procedures that           *
!   will be used in the report                                                 *
!*******************************************************************************
