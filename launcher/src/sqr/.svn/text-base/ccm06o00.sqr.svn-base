!*****************************************************************************
!  STAFF TODO REPORT                                                         *
!*****************************************************************************  

!*****************************************************************************
!                                                                            *
! PROGRAM NAME:         ccm06o00.sqr                                         *
!                                                                            *
! PROGRAM LOCATION:     /sys/prod/sqr/ccm06o00.sqr                           *
!                                                                            *
!                                                                            *
! DATE:                 05/10/04                                             *
!                                                                            *
! PROGRAMMER NAME:      Susie Miller                                         *
!                                                                            *
! PROGRAM DESCRIPTION:  The Staff To-Do List Report will display the         *
!                       entire todo list for a user sorted by date, case,    *
!                       stage or type of todo.                               *
!                       The report will be generated via the Staff To-Do List*
!                       page.                                                *
!*****************************************************************************
!*****************************************************************************
!                                                                            *
! PROGRAM MODIFICATION SECTION                                               *
! ----------------------------                                               *
!                                                                            *
!   DATE     PROGRAMMER                       MODIFICATION                   *
! --------   ----------   -------------------------------------------------  *
! 06/21/05   Fraserkr     SIR23595: Add Outer Join to GET-TODO-INFO SQL, so  *
!                           to do's that are not associated with a stage     *
!                           will be on report.  Also, revised the wrap       *
!                           position of the print description line.          *
! 08/24/2005 Fraserkr     SIR23595:  change to wrap position is causing      *
!                           some page break issues and also causing data     *
!                           to overwrite the heading detail underscores on   *
!                           some pages. Change BEGIN-HEADING to 8 lines.     *
!                           Stop use of setup03a.sqc to minimize white space *
!                           on report.                                       * 
!09/20/2005  Malpans	  SIR23925: Added a footer to the Staff To-Do List   *
!			    Report	                                     *     
!									     *
!*****************************************************************************
!*****************************************************************************
!                                                                            *
! REPORT PROCESSING SECTION                                                  *
!                                                                            *
!       This procedure defines the beginning and end of the report proc-     *
!       essing. Housekeeping initializes all report values, Processing       *
!       processes the body of the report, and Wrap-Up prints the end of re-  *
!       port message and other wrap up functions.                            *
!                                                                            *
!*****************************************************************************

BEGIN-REPORT

    do HOUSEKEEPING

    do PROCESS-REPORT        

    do WRAP-UP

END-REPORT

!*****************************************************************************
!  SETUP-SECTION                                                             *
!                                                                            *
!       This section sets up common parameters including page size and       *
!       printer as well as input parameters                                  *
!                                                                            *
!*****************************************************************************
BEGIN-SETUP

    #ifdef ORACLE                           !compiler directive
        ask dbname 'Database Name'
        use {dbname}
    #endif

    ! Printer and Page-Size Initialization 
    ! For ON DEMAND Reports which are Landscape
    ! and use Courier New Font of size 10
    ! see additional comments in setup03a.sqc
    !#INCLUDE 'setup03a.sqc'      ! control codes for Landscape 

    ! standard setup03a provides 39 lines, too much white space
    !   Dee Lopez authorized change to 50 lines per page, to reduce white 
    !   space.  Still have room for 3-hole punched paper.
     page-size 50 122
    

    ask ID_TODO_PERS_ASSIGNED          ! input parameter
    ask ORDER_BY                       ! input parameter
END-SETUP

!*****************************************************************************
!                                                                            *
! HEADER                                                                     *
!                                                                            *
!       These commands will be processed every time SQR writes a page. They  *
!       include the report title, date, time, page number, report id, and    *
!       column headers. XXX lines are reserved to print the heading at the   *
!       top of each page. This includes blank lines. A common setup header   *
!       has been created called XXXXXXXXXXXXXXXXXXX for landscape or portrait*
!       reports.                                                             *
!                                                                            *
!*****************************************************************************

BEGIN-HEADING 8
print 'Department of Family and Protective Services' (0) center
print 'Staff To-Do List Report'                     (+1) center
print 'Ordered By:'                                 (+1,48)  
#IF '{ORDER_BY}' = '1'
print 'Todo Due Date'                             (+,62)  
#ELSE
#IF '{ORDER_BY}' = '2'
print 'CASE ID'                                   (+,62)  
#ELSE
#IF '{ORDER_BY}' = '3'
Print  'Stage Name'                               (+,62)  
#ELSE
#IF '{ORDER_BY}' = '4'
print 'Todo Creator'                              (+,62)  
#END-IF        
#END-IF
#END-IF
#END-IF
do PRINT-TITLE


END-HEADING

!*****************************************************************************
!                                                                            *
! FOOTER                                                                     *
!                                                                            *
!       These commands will be processed every time SQR writes a page. They  *
!       include a footer that must be in place in all pages of some reports  *
!                                                                            *
!*****************************************************************************

BEGIN-FOOTING 3

  #INCLUDE 'footer04.sqc'       ! Include file with footer information

END-FOOTING

!****************************************************************************
!                                                                           *
!   HOUSEKEEPING                                                            *
!                                                                           *
!       Set environment variables, initialize report id, initialize title   *
!       id, initialize date and time, initialize variables, and define      *
!       constants                                                           *
!                                                                           *
!       CALLED BY: BEGIN-REPORT                                             *
!                                                                           * 
!****************************************************************************

BEGIN-PROCEDURE HOUSEKEEPING

   #INCLUDE 'setenv.sqc'                    !Set printer environment
    
    move {ID_TODO_PERS_ASSIGNED}                        to #input_id_person
        move {ORDER_BY}                                 to #INPUT_ORDER


END-PROCEDURE HOUSEKEEPING

!****************************************************************************
!                                                                           *
!   PROCESS-REPORT                                                          *
!                                                                           *
!      This procedure includes the main processing logic of the report.     *
!                                                                           *
!       CALLED BY  : BEGIN-REPORT                                           *
!       CALLS      : GET-CURRENT-INFO, GET-TODO-INFO                        *
!****************************************************************************

BEGIN-PROCEDURE PROCESS-REPORT 

move 0      to #count                 
    do GET-CURRENT-INFO
    do GET-TODO-INFO

END-PROCEDURE PROCESS-REPORT                   

!****************************************************************************
!                                                                           *
!   GET-CURRENT-INFO                                                        *
!                                                                           *
!       This procedure will retrieve the most current primary name and      *
!       information for the input person.                                   *
!                                                                           * 
!       CALLED BY  : PROCESS-REPORT                                         *
!       CALLS      :                                                        *
!****************************************************************************

BEGIN-PROCEDURE GET-CURRENT-INFO

BEGIN-SELECT ON-ERROR=SqlErrorHandle

A.NM_NAME_FIRST 
    move &A.NM_NAME_FIRST                   to $person_first
    
A.NM_NAME_MIDDLE
    move &A.NM_NAME_MIDDLE                  to $person_middle     
    
A.NM_NAME_LAST
    move &A.NM_NAME_LAST                    to $person_last

CSUFFIX2A.decode
    move &CSUFFIX2A.decode                  to $person_suffix

FROM    NAME        A,
        PERSON      B,
        CSUFFIX2    CSUFFIX2A  
        
WHERE   A.ID_PERSON = #input_id_person
AND     B.ID_PERSON = A.ID_PERSON
AND     A.IND_NAME_PRIMARY = 'Y'
AND     A.DT_NAME_END_DATE= '12/31/4712'
AND     CSUFFIX2A.CODE(+) = A.CD_NAME_SUFFIX   
                     
END-SELECT

END-PROCEDURE GET-CURRENT-INFO
       


           
!****************************************************************************
!                                                                           *
!   GET-TODO-INFO                                                           *
!                                                                           *
!       This procedure will retrieve all the Staff To-Dos that the          *
!       person has on the todo list page.                                   *
!                                                                           * 
!       CALLED BY  : PROCESS-REPORT                                         *
!       CALLS      :  PRINT-TODO                                            *
!                                                                           *
!   SIR23595:  added outer join on link between s.id_stage & id_todo_stage  *
!****************************************************************************

BEGIN-PROCEDURE GET-TODO-INFO

BEGIN-SELECT ON-ERROR=SqlErrorHandle

CTODOTYP.decode
   move &CTODOTYP.decode                      to $cd_todo_task
   
ID_TODO_CASE
   move &ID_TODO_CASE                         to $id_case
   
ID_TODO_PERS_CREATOR
   move &ID_TODO_PERS_CREATOR                 to #id_todo_creator
   
TXT_TODO_DESC 
   move &TXT_TODO_DESC                        to $desc
   
DT_TODO_DUE
   move &DT_TODO_DUE                          to $dt_todo_task_due
   
NM_TODO_CREATOR_INIT
   move &NM_TODO_CREATOR_INIT                 to $creator_init
   
S.NM_STAGE     
   move &S.NM_STAGE                           to $nm_stage
   
  do PRINT-TODO
      !let #count = #count + 1    SIR23595 commented out
      !if #count = 55             let sqr handle page-breaking
      !   NEW-PAGE
      !   move 0      to #count
      !end-if

FROM    TODO,
        STAGE S,
        CTODOTYP
        
WHERE   ID_TODO_PERS_ASSIGNED = #input_id_person
and     s.id_stage(+) = id_todo_stage
and     CTODOTYP.code(+) = cd_todo_type
and     dt_todo_completed is null
#IF '{ORDER_BY}' = '1'
        ORDER BY DT_TODO_DUE
#ELSE
#IF '{ORDER_BY}' = '2'
        ORDER BY ID_TODO_CASE
#ELSE
#IF '{ORDER_BY}' = '3'
        ORDER BY NM_STAGE
#ELSE
#IF '{ORDER_BY}' = '4'
        ORDER BY NM_TODO_CREATOR_INIT
#END-IF        
#END-IF
#END-IF
#END-IF

                            
END-SELECT

END-PROCEDURE GET-TODO-INFO

!****************************************************************************
!                                                                           *
!   PRINT-TITLE                                                             *
!                                                                           *
!       This procedure prints the Emoployee information.                    *
!                                                                           * 
!       CALLED BY  : HEADER                                                 *
!       CALLS      : NONE                                                   *
!****************************************************************************

 BEGIN-PROCEDURE PRINT-TITLE  

 print 'Employee Name: '                        (+1,1)
 if $person_suffix <> ''
    let $person_name_suff = ', ' || $person_suffix
 end-if
 if $person_middle = '' 
    let $person_name = $person_first || ' ' || $person_last || $person_name_suff
 else
    let $person_name = $person_first || ' ' || $person_middle || ' ' || $person_last || $person_name_suff
 end-if 
 print $person_name                              ()
 move '' to $person_name_suff                   
 
 print 'Employee ID: '                           (+1,1)
 print #input_id_person                          () edit 8888888888888888
  
 print 'Type'                                    (+2,1)
 print 'Creator'                                 (+0,8)
 print 'Due Date'                                (+0,17)
 print 'Stage Name'                              (+0,28)
 print 'Case ID'                                 (+0,52)
 print 'Description'                             (+0,63)
 
 print '-'                                       (+1,1,120) fill 
 END-PROCEDURE PRINT-TITLE 


!****************************************************************************
!                                                                           *
!   PRINT-TODO                                                              *
!                                                                           *
!       This procedure prints the todo summary information.                 *
!                                                                           * 
!       CALLED BY  : GET-TODO-INFO                                          *
!       CALLS      : NONE                                                   *
!                                                                           *
!   SIR23595:  Changed wrap column for print of $desc from 80 to 58         *
!****************************************************************************

BEGIN-PROCEDURE PRINT-TODO  
!next-listing skiplines=0 need=1

        print    $cd_todo_task                   (+1,1) 
        IF $creator_init = '' 
        and $cd_todo_task = 'Task'
        print 'System'                           (+0,8)
        else
        print    $creator_init                   (+0,8)
        end-if
        print    $dt_todo_task_due               (+0,17)
        print    $nm_stage                       (+0,28)      
        print    $id_case                        (+0,52)
        print    $desc                           (+0,63) wrap 58 2

!show $creator_init
!show $cd_todo_task

END-PROCEDURE PRINT-TODO 

!****************************************************************************
!                                                                           *
!   WRAP-UP                                                                 *
!                                                                           *
!       This procedure prints the messages for the end of the report as well*
!       as over all totals.                                                 *
!                                                                           * 
!       CALLED BY  : BEGIN-REPORT                                           *
!       CALLS      : NONE                                                   *
!****************************************************************************

BEGIN-PROCEDURE WRAP-UP

   NEW-PAGE

END-PROCEDURE WRAP-UP


!***********************************************************************
!                                                                      *
! SqlErrorHandle                                                       *
!                                                                      *
!      This section handles formatting of messages for SQL errors.     *
!      When an SQL error is encounterd, the program will write to      * 
!      the log file, program processing does not stop for SQL errors.  *
!                                                                      *
!  CALLS:  None                                                        *
!                                                                      *
!***********************************************************************

BEGIN-PROCEDURE SqlErrorHandle 


  print 'SQL error - Processing Terminated'               (+1,2)
  print $error                                            (+1,2)
  print 'ERROR: SQL error halted loader processing'       (+0)
  print $error                                            (+1,2)
  print 'SQL Error: '                                     (+0)
  print $sql-error                                        (+0)
  print $error                                            (+1,2)
  print 'SQL Status: '                                    (+0)
  print #sql-status                                       (+0)
  STOP

END-PROCEDURE SqlErrorHandle


!****************************************************************************
!                                                                           *
!                                INCLUDE SECTION                            *
!                                                                           *
!   Include all files that contain common modules or procedures that        *
!   will be used in the report                                              *
!****************************************************************************
#INCLUDE 'cfpstamp.sqc'
